plugins {
    id 'java'
    id 'maven'
}

group = 'ch.loway.oss.ari4java'
version = '0.5.1'

sourceSets {
    main {
        java {
            srcDir 'src/main/generated'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.6'
    compile 'io.netty:netty-all:4.0.25.Final'
    compile 'javax.xml.bind:jaxb-api:2.1'

    testCompile 'junit:junit:4.10'
}

compileJava.dependsOn ':codegen:runCodegen'
compileJava.dependsOn 'buildProps'

def build_number = 'x'
if (System.getenv('BUILD_NUMBER') != null) {
    build_number = System.getenv('BUILD_NUMBER')
}
task buildProps(type: WriteProperties) {
    outputFile file('src/main/resources/build.properties')
    property 'BUILD_NUMBER', build_number
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version
    }
}
